#version 460 core

layout (local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

layout (rg16f) readonly uniform image3D uGrid;
layout (rg16f) writeonly uniform image3D uOutputGrid;
layout (r16f) readonly uniform image2D uWeights;

uniform uint uBlurRadius;

void BilateralGridToneMappingPass1(
    ivec3 coord, uint blurRadius
) {
    ivec3 gridSize = imageSize(uGrid);
    int left = max(coord.x - int(blurRadius), 0);
    int right = min(coord.x + int(blurRadius), gridSize.x - 1);

    vec2 total = vec2(0);
    for (int i = left; i <= right; i++) {
        total += imageLoad(uGrid, ivec3(i, coord.y, coord.z)).rg * 
            imageLoad(uWeights, ivec2(coord.x - i + blurRadius, 0)).r;
    }

    imageStore(uOutputGrid, coord, vec4(total, 0, 0));
}

void main() {
    ivec3 gridSize = imageSize(uGrid);
    if (gl_GlobalInvocationID.x >= gridSize.x || 
        gl_GlobalInvocationID.y >= gridSize.y ||
        gl_GlobalInvocationID.z >= gridSize.z) {
        return;
    }
    BilateralGridToneMappingPass1(ivec3(gl_GlobalInvocationID), uBlurRadius);
}
